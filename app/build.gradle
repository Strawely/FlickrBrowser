plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
}

apply from: "${rootDir}/gradle/dependencies.gradle"

def properties = apikeyProperties()
def apikey = properties['apikey']

def apikeyProperties() {
    def apikeyFile = new File("${rootDir}/apikey.properties")
    Properties apikeyProps = new Properties()
    if (apikeyFile.canRead()) {
        apikeyProps.load(new FileInputStream(apikeyFile))
    }
    return apikeyProps
}

android {
    compileSdk sdkVersions.compile

    defaultConfig {
        applicationId "ru.solom.flickrbrowser"
        minSdk sdkVersions.min
        targetSdk sdkVersions.target
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField "String", "API_KEY", apikey
        }

        debug {
            buildConfigField "String", "API_KEY", apikey
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion versions.compose
    }
}

dependencies {
    implementation project(":core")
    implementation project(":data")

    implementation 'androidx.core:core-ktx:1.6.0'
    implementation 'androidx.appcompat:appcompat:1.3.1'

    implementation deps.compose.foundation
    implementation deps.compose.material

    implementation deps.squareup.moshi
    implementation deps.squareup.retrofit2
    implementation deps.squareup.moshi_converter
    implementation deps.squareup.moshi_kotlin
    implementation deps.squareup.logging_interceptor

    implementation deps.google.hilt
    kapt deps.google.hilt_kapt

    implementation deps.androidx.viewModelKtx
    implementation deps.androidx.activityKtx

    implementation deps.coil
}
